
// --------------------------------------------------------------------------------
// Copyright (c) 2024-2025 Igor Ryabov (https://github.com/IgorRyaboff/Instrumentiki)
// License: https://github.com/IgorRyaboff/Instrumentiki/blob/main/LICENSE
// --------------------------------------------------------------------------------

// Параметры формы:
//  ПараметрыСоединенияСИБ - Структура - См. НовыеПараметрыСоединенияСИБ формы АдминистрированиеСервера
//  ЗапрашиваемоеИмяИБ - Строка - Если заполнено, у пользователя будет запрошен ручной ввод имени ИБ
//                       Может применяться для подтверждения опасных действий (например, удаления ИБ)
//
// Параметр закрытия:
//  Неопределено - При нажатии "Отмена"
//  Структура - Заполненная структура из параметра ПараметрыСоединенияСИБ

#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПараметрыСоединенияСИБ = Параметры.ПараметрыСоединенияСИБ;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыСоединенияСИБ, "Логин, Пароль");
	
	Если ЗначениеЗаполнено(Параметры.ЗапрашиваемоеИмяИБ) Тогда
		ИмяИБ = Параметры.ЗапрашиваемоеИмяИБ;
		Элементы.ИмяИБОтПользователя.ПодсказкаВвода = ИмяИБ;
		Элементы.ФормаОК.Заголовок = НСтр("ru='Далее'");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПарольНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Элемент.РежимПароля = Не Элемент.РежимПароля;
	Картинка = ?(Элемент.РежимПароля, БиблиотекаКартинок.ПоказатьПароль, БиблиотекаКартинок.СкрытьПароль);
	Элемент.КартинкаКнопкиВыбора = Картинка;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаЛогинПароль Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыСоединенияСИБ, ЭтотОбъект, "Логин, Пароль");
		
		Если ЗначениеЗаполнено(ИмяИБ) Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПроверкаИмениИБ;
			Элементы.ФормаОК.Заголовок = "";
		Иначе
			Закрыть(ПараметрыСоединенияСИБ);
		КонецЕсли;
	Иначе
		Если ВРег(ИмяИБ) <> ВРег(ИмяИБОтПользователя) Тогда
			ПоказатьПредупреждение(, НСтр("ru='Имя ИБ введено неверно'"));
			Возврат;
		КонецЕсли;
		
		Закрыть(ПараметрыСоединенияСИБ);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// Код процедур и функций
#КонецОбласти
