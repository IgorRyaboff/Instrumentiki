
// --------------------------------------------------------------------------------
// Copyright (c) 2024-2025 Igor Ryabov (https://github.com/IgorRyaboff/Instrumentiki)
// License: https://github.com/IgorRyaboff/Instrumentiki/blob/main/LICENSE
// --------------------------------------------------------------------------------

// Параметры формы:
//  ПараметрыСоединенияСКластером - Структура - См. НовыеПараметрыСоединенияСКластером формы АдминистрированиеСервера
//                                  Заполняемая в данной форме структура параметров соединения с кластером
//
// Параметр закрытия:
//  Неопределено - нажатие кнопки "Отмена"
//  Структура - Заполненные параметры соединения с кластером
//              Структура остается та же, что и в параметре ПараметрыСоединенияСКластером

#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПараметрыСоединенияСКластером = Параметры.ПараметрыСоединенияСКластером;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыСоединенияСКластером, "МетодПодключения, АдресАгента, ПортАгента, ЛогинАгента, ПарольАгента
	                                                                    |, ПортКластера, ЛогинКластера, ПарольКластера");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПарольАгентаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПоказатьСкрытьПароль(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПарольКластераНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПоказатьСкрытьПароль(Элемент);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ПараметрыСоединенияСКластером, ЭтотОбъект, "МетодПодключения, АдресАгента, ПортАгента
	                                                                    |, ЛогинАгента, ПарольАгента
	                                                                    |, ПортКластера, ЛогинКластера, ПарольКластера");
	Закрыть(ПараметрыСоединенияСКластером);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСохраненныеПараметры(Команда)
	УдалитьСохраненныеПараметрыНаСервере();
	ПоказатьПредупреждение(, НСтр("ru='Сохраненные параметры для текущего пользователя удалены'"));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УдалитьСохраненныеПараметрыНаСервере()
	РеквизитФормыВЗначение("Объект").СохранитьНастройку("АдминистрированиеСервера", "ПараметрыСоединенияСКластером", Неопределено);
КонецПроцедуры

// Переключает режим пароля, а также картинку кнопки выбора элемента
//
// Параметры:
//  Элемент - ПолеФормы -
&НаКлиенте
Процедура ПоказатьСкрытьПароль(Элемент)
	Элемент.РежимПароля = Не Элемент.РежимПароля;
	Элемент.КартинкаКнопкиВыбора = ?(Элемент.РежимПароля, БиблиотекаКартинок.ПоказатьПароль, БиблиотекаКартинок.СкрытьПароль);
КонецПроцедуры

#КонецОбласти
