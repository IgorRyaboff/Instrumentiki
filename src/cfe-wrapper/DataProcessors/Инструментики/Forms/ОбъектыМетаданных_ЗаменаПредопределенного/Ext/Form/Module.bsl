
// --------------------------------------------------------------------------------
// Copyright (c) 2024-2025 Igor Ryabov (https://github.com/IgorRyaboff/Instrumentiki)
// License: https://github.com/IgorRyaboff/Instrumentiki/blob/main/LICENSE
// --------------------------------------------------------------------------------

#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Параметры.Свойство("Значение", СтароеЗначение);
	Параметры.Свойство("ИмяЗначения", ИмяЗначения);
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(ТипЗнч(СтароеЗначение));
	Элементы.НовоеЗначение.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
	
	Элементы.НовоеЗначение.ВыбиратьТип = Ложь;
	
	ПометитьСтароеЗначениеНаУдаление = ЗначениеЗаполнено(СтароеЗначение);
	Элементы.ПометитьСтароеЗначениеНаУдаление.Доступность = ЗначениеЗаполнено(СтароеЗначение);
	
	Если Не СтароеЗначение.Пустая() Тогда
		Попытка
			СтароеЗначение.ПолучитьОбъект();
		Исключение
			СтароеЗначениеБитое = Истина;
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если СтароеЗначениеБитое Тогда
		ПоказатьПредупреждение(, "Похоже, что сейчас к значению привязана битая ссылка");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура Заменить(Команда)
	Если Не ЗначениеЗаполнено(СтароеЗначение) И Не ЗначениеЗаполнено(НовоеЗначение) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указано новое значение";
		Сообщение.Поле = "";
		Сообщение.УстановитьДанные();
		Сообщение.Сообщить();
		Возврат;
	ИначеЕсли Не ЗначениеЗаполнено(НовоеЗначение) Тогда
		Ответ = Ждать ВопросАсинх("Не указано новое значение. Нажмите ""Да"", чтобы отвязать старое значение от предопределённого без привязки нового", РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ИмяПредопределенногоДляНовогоЗначения = ИмяПредопределенногоДляОбъекта(НовоеЗначение);
	Если ЗначениеЗаполнено(ИмяПредопределенногоДляНовогоЗначения) Тогда
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, "Продолжить");
		Кнопки.Добавить(КодВозвратаДиалога.Нет, "Отменить");
		
		ТекстВопроса = СтрШаблон("Новое значение уже привязанно к предопределённому значению ""%1"". Если продолжить, то связь с ним будет утеряна", ИмяПредопределенногоДляНовогоЗначения);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, Кнопки);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаменитьНаСервере(СтароеЗначение, НовоеЗначение, ИмяЗначения, ПометитьСтароеЗначениеНаУдаление);
	Ждать ПредупреждениеАсинх("Готово!");
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает значение стандартного реквизита ИмяПредопределенныхДанных для переданной ссылки
//
// Параметры:
//  Ссылка - СправочникСсылка, ПланВидовХарактеристикСсылка, ПланСчетовСсылка, ПланВидовРасчетаСсылка -
//
// Возвращаемое значение:
//  Строка, Неопределено -
&НаСервереБезКонтекста
Функция ИмяПредопределенногоДляОбъекта(Ссылка)
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Таблица.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных
		|ИЗ
		|	&Таблица КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка = &Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Таблица", Ссылка.Метаданные().ПолноеИмя());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.ИмяПредопределенныхДанных;
	КонецЦикла;
КонецФункции

//
// Параметры:
//  СтароеЗначение - СправочникСсылка, ПланВидовХарактеристикСсылка, ПланСчетовСсылка, ПланВидовРасчетаСсылка -
//  НовоеЗначение - СправочникСсылка, ПланВидовХарактеристикСсылка, ПланСчетовСсылка, ПланВидовРасчетаСсылка -
//  ИмяЗначения - Строка -
//  ПометстьСтароеНаУдаление - Булево -
&НаСервереБезКонтекста
Процедура ЗаменитьНаСервере(СтароеЗначение, НовоеЗначение, ИмяЗначения, ПометитьСтароеНаУдаление)
	НачатьТранзакцию();
	Попытка
		Если ЗначениеЗаполнено(СтароеЗначение) Тогда
			Объект = СтароеЗначение.ПолучитьОбъект();
			Объект.ИмяПредопределенныхДанных = Неопределено;
			Объект.Записать();
			Если ПометитьСтароеНаУдаление Тогда
				Объект.УстановитьПометкуУдаления(Истина);
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НовоеЗначение) Тогда
			Объект = НовоеЗначение.ПолучитьОбъект();
			Если Объект.ПометкаУдаления Тогда
				Объект.УстановитьПометкуУдаления(Ложь);
			КонецЕсли;
			Объект.ИмяПредопределенныхДанных = ИмяЗначения;
			Объект.Записать();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

#КонецОбласти
