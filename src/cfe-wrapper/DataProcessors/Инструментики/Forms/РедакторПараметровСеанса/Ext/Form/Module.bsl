
// --------------------------------------------------------------------------------
// Copyright (c) 2024-2025 Igor Ryabov (https://github.com/IgorRyaboff/Instrumentiki)
// License: https://github.com/IgorRyaboff/Instrumentiki/blob/main/LICENSE
// --------------------------------------------------------------------------------

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьТаблицуПСНаСервере();
	Элементы.ПривилегированныйРежим.Доступность = Не БезопасныйРежим();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаПС

&НаКлиенте
Процедура ТаблицаПСЗначениеПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ТаблицаПС.ТекущиеДанные;
	УстановитьНовоеЗначениеПС(ТекущиеДанные.Имя, ТекущиеДанные.Значение, ПривилегированныйРежим);
	Состояние("Значение установлено",, ТекущиеДанные.Синоним);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКонстантВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтрТаблицаПС = Элементы.ТаблицаПС.ТекущиеДанные;
	
	Если Поле.Имя = "ТаблицаПСЗначение" И ТипЗнч(СтрТаблицаПС.Значение) = Тип("УникальныйИдентификатор") Тогда
		СтандартнаяОбработка = Ложь;
		ИзменитьУникальныйИдентификатор(СтрТаблицаПС);
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПСПередНачаломИзменения(Элемент, Отказ)
	ТекДанные = Элемент.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекДанные.НередактируемыйТип Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьТаблицуПС(Команда)
	ТаблицаПС.Очистить();
	ЗаполнитьТаблицуПСНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьВКонсолиКода(Команда)
	ТекДанные = Элементы.ТаблицаПС.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоХранилищеЗначения = (ТипЗнч(ТекДанные.Значение) = Тип("ХранилищеЗначения"));
	
	ШаблонКода = "Значение = ПараметрыСеанса.%1%2;
	             |
				 |// Обработка значения
				 |
				 |ПараметрыСеанса.%1 = %3;";
	Код = СтрШаблон(ШаблонКода, ТекДанные.Имя, ?(ЭтоХранилищеЗначения, ".Получить()", ""), ?(ЭтоХранилищеЗначения, "Новый ХранилищеЗначения(Значение)", "Значение"));
	
	ПараметрыФормы = Новый Структура("Код, НаКлиенте", Код, Ложь);
	ОткрытьФорму(Объект.ИмяМетаданныхОбработки + ".Форма.КонсольКода", ПараметрыФормы);
КонецПроцедуры

&НаКлиенте
Процедура СортироватьВозр(Команда)
	СортироватьТаблицуЗначений("ТаблицаПС", "Возр");
КонецПроцедуры

&НаКлиенте
Процедура СортироватьУбыв(Команда)
	СортироватьТаблицуЗначений("ТаблицаПС", "Убыв");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Устанавливает значение параметра сеанса
//
// Параметры:
//  Имя - Строка - Имя параметра сеанса
//  Значение - Произвольный - Значение параметра сеанса
//  ПривилегированныйРежим - Булево - Если Истина, то установка значения будет выполнена в привилегированном режиме
&НаСервереБезКонтекста
Процедура УстановитьНовоеЗначениеПС(Знач Имя, Знач Значение, Знач ПривилегированныйРежим)
	Если ПривилегированныйРежим Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	ПараметрыСеанса[Имя] = Значение;
КонецПроцедуры

&НаКлиенте
Процедура ВызватьОбновлениеТаблицы()
	ИдентификаторСтроки = Элементы.ТаблицаПС.ТекущаяСтрока;
	Если ЗначениеЗаполнено(ИдентификаторСтроки) Тогда
		ТекущаяКонстанта = Элементы.ТаблицаПС.ТекущиеДанные.Имя;
	Иначе
		ТекущаяКонстанта = Неопределено;
	КонецЕсли;
	
	ЗаполнитьТаблицуПСНаСервере();
	
	Если ТекущаяКонстанта <> Неопределено Тогда
		СтрТаблица = ТаблицаПС.НайтиСтроки(Новый Структура("Имя", ТекущаяКонстанта))[0];
		Элементы.ТаблицаПС.ТекущаяСтрока = СтрТаблица.ПолучитьИдентификатор();
	КонецЕсли;
КонецПроцедуры

// Заполняет таблицу формы ТаблицаПС
//
&НаСервере
Процедура ЗаполнитьТаблицуПСНаСервере()
	ТаблицаПС.Очистить();
	
	Если ПривилегированныйРежим Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	Для Каждого ПС Из Метаданные.ПараметрыСеанса Цикл
		Стр = ТаблицаПС.Добавить();
		Стр.Имя = ПС.Имя;
		Стр.Синоним = ПС.Синоним;
		Стр.ТипЗначения = Метаданные.ПараметрыСеанса.Найти(ПС.Имя).Тип;
		Попытка
			ЗначениеПС = ПараметрыСеанса[ПС.Имя];
			Если ТипЗнч(ЗначениеПС) = Тип("ХранилищеЗначения")
				Или ТипЗнч(ЗначениеПС) = Тип("ДвоичныеДанные")
				Или ТипЗнч(ЗначениеПС) = Тип("Null")
				Или ТипЗнч(ЗначениеПС) = Тип("ОписаниеТипов")
				Или ТипЗнч(ЗначениеПС) = Тип("ФиксированныйМассив")
				Или ТипЗнч(ЗначениеПС) = Тип("ФиксированнаяСтруктура")
				Или ТипЗнч(ЗначениеПС) = Тип("ФиксированноеСоответствие") Тогда
				Стр.Значение = Неопределено;
				Стр.НередактируемыйТип = Истина;
			Иначе
				Стр.Значение = ЗначениеПС;
			КонецЕсли;
		Исключение
			Стр.Значение = Неопределено;
			Стр.ОшибкаПолучения = Истина;
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры

// Универсальная процедура сортировки таблицы значений
//
// Параметры:
//  ИмяТаблицыФормы - Строка - Имя элемента формы, ссылающегося на таблицу
//  Направление - Строка - "Возр" или "Убыв"
&НаСервере
Процедура СортироватьТаблицуЗначений(ИмяТаблицыФормы, Направление)
	ТаблицаФормы = Элементы[ИмяТаблицыФормы];
	ПрефиксПутиКДанным = ТаблицаФормы.ПутьКДанным + ".";
	
	ТекущаяКолонка = ТаблицаФормы.ТекущийЭлемент;
	ИмяКолонки = СтрЗаменить(ТекущаяКолонка.ПутьКДанным, ПрефиксПутиКДанным, "");
	
	ЭтотОбъект[ТаблицаФормы.ПутьКДанным].Сортировать(ИмяКолонки + " " + Направление);
КонецПроцедуры

&НаКлиенте
Асинх Процедура ИзменитьУникальныйИдентификатор(СтрокаТаблицы)
	СтароеЗначение = СтрокаТаблицы.Значение;
	НовоеЗначениеСтрокой = Ждать ВвестиСтрокуАсинх(СтароеЗначение, НСтр("ru='Введите значение'"), 36);
	Если НовоеЗначениеСтрокой = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		НовоеЗначение = Новый УникальныйИдентификатор(НовоеЗначениеСтрокой);
	Исключение
		ПоказатьПредупреждение(, НСтр("ru='Введенное значение не является уникальным идентификатором'"));
		Возврат;
	КонецПопытки;
	
	УстановитьНовоеЗначениеПС(СтрокаТаблицы.Имя, НовоеЗначение, ПривилегированныйРежим);
	ВызватьОбновлениеТаблицы();
КонецПроцедуры

#КонецОбласти
