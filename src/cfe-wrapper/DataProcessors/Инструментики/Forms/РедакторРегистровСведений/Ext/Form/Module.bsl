
// --------------------------------------------------------------------------------
// Copyright (c) 2024-2025 Igor Ryabov (https://github.com/IgorRyaboff/Instrumentiki)
// License: https://github.com/IgorRyaboff/Instrumentiki/blob/main/LICENSE
// --------------------------------------------------------------------------------

#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Для Каждого Мета Из Метаданные.РегистрыСведений Цикл
		Если Мета.РежимЗаписи <> Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.Независимый Тогда
			Продолжить;
		КонецЕсли;
		
		Синоним = ?(ЗначениеЗаполнено(Мета.Синоним), Мета.Синоним, Мета.Имя);
		Элементы.ВыбранныйРегистр.СписокВыбора.Добавить(Мета.Имя, Синоним);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если Модифицированность Тогда
		Отказ = Истина;
		
		Если Не ЗавершениеРаботы Тогда
			ЗапроситьСохранениеАсинх(Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВыбранныйРегистрПриИзменении(Элемент)
	Отбор.Очистить();
	
	Для Каждого КлючЗнч Из ИзмеренияРегистраСведений(ВыбранныйРегистр) Цикл
		ИмяИзмерения = КлючЗнч.Ключ;
		ДанныеИзмерения = КлючЗнч.Значение;
		
		СтрОтбор = Отбор.Добавить();
		СтрОтбор.ИмяПоля = ИмяИзмерения;
		СтрОтбор.Синоним = ДанныеИзмерения.Синоним;
		СтрОтбор.Синоним = ДанныеИзмерения.Синоним;
		СтрОтбор.Тип = ДанныеИзмерения.Тип;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтбор

&НаКлиенте
Процедура ОтборЗначениеПриИзменении(Элемент)
	ТекДанные = Элементы.Отбор.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекДанные.Использование = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСодержимоеНабораЗаписей

&НаКлиенте
Процедура СодержимоеНабораЗаписейПриИзменении(Элемент)
	ЗначенияОтбора = Новый Структура;
	Для Каждого СтрОтбор Из Отбор Цикл
		Если СтрОтбор.Использование Тогда
			ЗначенияОтбора.Вставить(СтрОтбор.ИмяПоля, СтрОтбор.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрНабора Из СодержимоеНабораЗаписей Цикл
		Для Каждого КлючЗнч Из ЗначенияОтбора Цикл
			ИмяПоля = КлючЗнч.Ключ;
			Значение = КлючЗнч.Значение;
			
			СтрНабора[ИмяПоля] = Значение;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СодержимоеНабораЗаписейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтрНабораЗаписей = Элемент.ТекущиеДанные;
	ИмяРеквизита = СтрРазделить(ПутьКДаннымЭлементаФормы(Поле.Имя), ".")[1];
	
	Если ТипЗнч(СтрНабораЗаписей[ИмяРеквизита]) = Тип("УникальныйИдентификатор") Тогда
		ИзменитьУникальныйИдентификатор(СтрНабораЗаписей, ИмяРеквизита);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ПеречитатьНаборЗаписей(Команда)
	Если Не ЧитатьТекущиеЗаписиРегистра(РежимЗамещенияПриЗаписи, Объект.ВерсияПлатформы) Тогда
		ПоказатьПредупреждение(, "Работа производится в режиме добавления новых строк");
		Возврат;
	КонецЕсли;
	
	Если Не (Ждать ЗапроситьСохранениеАсинх(Ложь)) Тогда
		Возврат;
	КонецЕсли;
	
	Состояние("Перечитываем...");
	ПеречитатьНаборЗаписейНаСервере();
	Состояние("Набор записей перечитан");
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗаписатьНаборЗаписей(Команда)
	Если РежимЗамещенияПриЗаписи = Истина Тогда
		Ответ = Ждать ВопросАсинх(НСтр("ru = 'Все существующие записи регистра (в пределах установленного отбора) будет УДАЛЕНЫ и заменены набором записей, который вы видите перед собой.
                                        |Продолжить?'"), РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
		
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если РежимЗамещенияПриЗаписи <> Истина И СодержимоеНабораЗаписей.Количество() = 0 Тогда
		ПоказатьПредупреждение(, "Набор записей пуст, записывать нечего");
		Возврат;
	КонецЕсли;
	
	Состояние("Записываем...");
	ЗаписатьНаборЗаписейНаСервере();
	Состояние("Набор записан!");
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПерейтиКНаборуЗаписей(Команда)
	Если Не ЗначениеЗаполнено(ВыбранныйРегистр) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не выбран регистр сведений";
		Сообщение.Поле = "ВыбранныйРегистр";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ПослеВыбораРежимаЗамещения", ЭтотОбъект);
	ОткрытьФорму(Объект.ИмяМетаданныхОбработки + ".Форма.РедакторРегистровСведений_ВыборРежимаЗамещения",,,,,,
		Обработчик);
	//
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВернутьсяКОтбору(Команда)
	Если Не (Ждать ЗапроситьСохранениеАсинх(Ложь)) Тогда
		Возврат;
	КонецЕсли;
	
	СодержимоеНабораЗаписей.Очистить();
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОтбор;
	Модифицированность = Ложь;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПутьКДаннымЭлементаФормы(Знач ИмяЭлемента)
	Возврат Элементы.Найти(ИмяЭлемента).ПутьКДанным;
КонецФункции

// Возвращает список измерений регистра для использования в отборах
//
// Параметры:
//  Имя - Строка - Имя регистра сведений
//
// Возвращаемое значение:
//  Структура - в качестве имен свойств передаются имена измерений. Значениями свойств выступает Структура:
//  * Синоним - Строка -
//  * Тип - ОписаниеТипов -
&НаСервереБезКонтекста
Функция ИзмеренияРегистраСведений(Имя)
	МетаданныеРегистра = Метаданные.РегистрыСведений[Имя];
	Результат = Новый Структура;
	
	Для Каждого Мета Из МетаданныеРегистра.Измерения Цикл
		Синоним = ?(ЗначениеЗаполнено(Мета.Синоним), Мета.Синоним, Мета.Имя);
		
		Результат.Вставить(Мета.Имя, Новый Структура("Синоним, Тип", Синоним, Мета.Тип));
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаСервере
Процедура ИнициализироватьНаборЗаписей()
	Пока Элементы.СодержимоеНабораЗаписей.ПодчиненныеЭлементы.Количество() > 0 Цикл
		Элементы.Удалить(Элементы.СодержимоеНабораЗаписей.ПодчиненныеЭлементы[0]);
	КонецЦикла;
	
	УдаляемыеРеквизиты = Новый Массив;
	Для Каждого Реквизит Из ПолучитьРеквизиты("СодержимоеНабораЗаписей") Цикл
		УдаляемыеРеквизиты.Добавить("СодержимоеНабораЗаписей." + Реквизит.Имя);
	КонецЦикла;
	ИзменитьРеквизиты(, УдаляемыеРеквизиты);
	
	МетаданныеРегистра = Метаданные.РегистрыСведений[ВыбранныйРегистр];
	
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавитьРеквизитыФормыИзКоллекцииРеквизитов(МетаданныеРегистра.СтандартныеРеквизиты, ДобавляемыеРеквизиты);
	ДобавитьРеквизитыФормыИзКоллекцииРеквизитов(МетаданныеРегистра.Измерения, ДобавляемыеРеквизиты);
	ДобавитьРеквизитыФормыИзКоллекцииРеквизитов(МетаданныеРегистра.Ресурсы, ДобавляемыеРеквизиты);
	ДобавитьРеквизитыФормыИзКоллекцииРеквизитов(МетаданныеРегистра.Реквизиты, ДобавляемыеРеквизиты);
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	ПоляУчаствующиеВОтборе = Новый Массив;
	Для Каждого СтрОтбор Из Отбор Цикл
		Если СтрОтбор.Использование Тогда
			ПоляУчаствующиеВОтборе.Добавить(СтрОтбор.ИмяПоля);
		КонецЕсли;
	КонецЦикла;
	
	ДобавитьПоляФормыИзКоллекцииРеквизитов(МетаданныеРегистра.СтандартныеРеквизиты,
		Элементы, Отбор, БиблиотекаКартинок.Реквизит);
	//
	ДобавитьПоляФормыИзКоллекцииРеквизитов(МетаданныеРегистра.Измерения,
		Элементы, Отбор, БиблиотекаКартинок.Измерение);
	//
	ДобавитьПоляФормыИзКоллекцииРеквизитов(МетаданныеРегистра.Ресурсы,
		Элементы, Отбор, БиблиотекаКартинок.Ресурс);
	//
	ДобавитьПоляФормыИзКоллекцииРеквизитов(МетаданныеРегистра.Реквизиты,
		Элементы, Отбор, БиблиотекаКартинок.Реквизит);
	//
	
	Если ЧитатьТекущиеЗаписиРегистра(РежимЗамещенияПриЗаписи, Объект.ВерсияПлатформы) Тогда
		ПеречитатьНаборЗаписейНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьРеквизитыФормыИзКоллекцииРеквизитов(КоллекцияРеквизитов, ДобавляемыеРеквизиты)
	Для Каждого РеквизитМетаданных Из КоллекцияРеквизитов Цикл
		Синоним = ?(ЗначениеЗаполнено(РеквизитМетаданных.Синоним), РеквизитМетаданных.Синоним, РеквизитМетаданных.Имя);
		Реквизит = Новый РеквизитФормы(РеквизитМетаданных.Имя,
			РеквизитМетаданных.Тип, "СодержимоеНабораЗаписей", Синоним, Истина);
		//
		ДобавляемыеРеквизиты.Добавить(Реквизит);
	КонецЦикла;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьПоляФормыИзКоллекцииРеквизитов(КоллекцияРеквизитов, Элементы, Отбор, КартинкаПоля)
	Для Каждого РеквизитМетаданных Из КоллекцияРеквизитов Цикл
		Элемент = Элементы.Добавить("СодержимоеНабораЗаписей" + РеквизитМетаданных.Имя, Тип("ПолеФормы"),
			Элементы.СодержимоеНабораЗаписей);
		//
		
		ЭтоБулевоеПоле = (РеквизитМетаданных.Тип = Новый ОписаниеТипов("Булево"));
		Элемент.Вид = ?(ЭтоБулевоеПоле, ВидПоляФормы.ПолеФлажка, ВидПоляФормы.ПолеВвода);
		
		Элемент.ПутьКДанным = "СодержимоеНабораЗаписей." + РеквизитМетаданных.Имя;
		Элемент.КартинкаШапки = КартинкаПоля;
		Элемент.Подсказка = РеквизитМетаданных.Имя;
		
		СтруктураОтбора = Новый Структура("ИмяПоля, Использование", РеквизитМетаданных.Имя, Истина);
		УчаствуетВОтборе = (Отбор.НайтиСтроки(СтруктураОтбора).Количество() > 0);
		Элемент.ТолькоПросмотр = УчаствуетВОтборе;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПеречитатьНаборЗаписейНаСервере()
	НаборЗаписей = НаборЗаписейСОтборами();
	НаборЗаписей.Прочитать();
	СодержимоеНабораЗаписей.Загрузить(НаборЗаписей.Выгрузить());
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаборЗаписейНаСервере()
	НаборЗаписей = НаборЗаписейСОтборами();
	НаборЗаписей.Записать(РежимЗамещенияПриЗаписи);
	
	Модифицированность = Ложь;
КонецПроцедуры

&НаСервере
Функция НаборЗаписейСОтборами()
	НаборЗаписей = РегистрыСведений[ВыбранныйРегистр].СоздатьНаборЗаписей();
	
	Для Каждого СтрОтбор Из Отбор Цикл
		Если СтрОтбор.Использование Тогда
			НаборЗаписей.Отбор.Найти(СтрОтбор.ИмяПоля).Установить(СтрОтбор.Значение, Истина);
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей.Загрузить(СодержимоеНабораЗаписей.Выгрузить());
	НаборЗаписей.ОбменДанными.Загрузка = ОбменДаннымиЗагрузка;
	Возврат НаборЗаписей;
КонецФункции

// При наличии несохраненных данных уточняет у пользователя необходимость продолжения действия с потерей данных
//
// Возвращаемое значение:
//  Булево - Действия, приводящие к потере данных, разрешены
&НаКлиенте
Асинх Функция ЗапроситьСохранениеАсинх(ЗакрытьФорму = Ложь)
	Если Не Модифицированность Тогда
		Возврат Истина;
	КонецЕсли;
	
	Ответ = Ждать ВопросАсинх("Изменения будут утеряны. Продолжить?", РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Результат = Истина;
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Результат = Ложь;
	КонецЕсли;
	
	Если Результат И ЗакрытьФорму Тогда
		Модифицированность = Ложь;
		ЭтотОбъект.Закрыть();
	КонецЕсли;
	Возврат Результат;
КонецФункции

// Сравнивает две версии
//
// Параметры
//  ВерсияСлева - Строка -
//  ВерсияСправа - Строка -
//
// Возвращаемое значение:
//  Число - "0", если версии совпадают; "1", если ВерсияСлева > ВерсияСправа; "-1", если ВерсияСлева < ВерсияСправа
&НаКлиентеНаСервереБезКонтекста
Функция СравнитьВерсии(ВерсияСлева, ВерсияСправа)
	ЧастиСлева = СтрРазделить(ВерсияСлева, ".");
	ЧастиСправа = СтрРазделить(ВерсияСправа, ".");
	КоличествоЧастей = ?(ЧастиСлева.Количество() > ЧастиСправа.Количество(), ЧастиСлева.Количество(), ЧастиСправа.Количество());
	
	Пока ЧастиСлева.Количество() < КоличествоЧастей Цикл
		ЧастиСлева.Добавить("0");
	КонецЦикла;
	Пока ЧастиСправа.Количество() < КоличествоЧастей Цикл
		ЧастиСправа.Добавить("0");
	КонецЦикла;
	
	Для Сч = 0 По КоличествоЧастей - 1 Цикл
		Слева = Число(ЧастиСлева[Сч]);
		Справа = Число(ЧастиСправа[Сч]);
		
		Если Слева > Справа Тогда
			Возврат -1;
		ИначеЕсли Слева < Справа Тогда
			Возврат 1;
		КонецЕсли;
	КонецЦикла;
	
	Возврат 0;
КонецФункции

&НаКлиенте
Асинх Процедура ИзменитьУникальныйИдентификатор(ИзменяемыйОбъект, ИмяИзменяемогоСвойства)
	СтароеЗначение = Строка(ИзменяемыйОбъект[ИмяИзменяемогоСвойства]);
	НовоеЗначениеСтрокой = Ждать ВвестиСтрокуАсинх(СтароеЗначение, НСтр("ru='Введите значение'"), 36);
	Если НовоеЗначениеСтрокой = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		НовоеЗначение = Новый УникальныйИдентификатор(НовоеЗначениеСтрокой);
	Исключение
		ПоказатьПредупреждение(, НСтр("ru='Введенное значение не является уникальным идентификатором'"));
		Возврат;
	КонецПопытки;
	
	ИзменяемыйОбъект[ИмяИзменяемогоСвойства] = НовоеЗначение;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораРежимаЗамещения(Режим, ДополнительныеПараметры = Неопределено) Экспорт
	Если Режим = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РежимЗамещенияПриЗаписи = Режим;
	
	Состояние("Подождите...");
	ИнициализироватьНаборЗаписей();
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСодержимоеНабораЗаписей;
	Состояние();
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЧитатьТекущиеЗаписиРегистра(ИспользуемыйРежимЗамещения, ВерсияПлатформы)
	Режимы = Новый Массив;
	Режимы.Добавить(Истина); // Добавление
	
	Если ВерсияПлатформы >= 3.25 Тогда
		Режимы.Добавить(ПредопределенноеЗначение("РежимЗамещения.Слияние"));
		Режимы.Добавить(ПредопределенноеЗначение("РежимЗамещения.Удаление"));
	КонецЕсли;
	
	Если ВерсияПлатформы >= 3.26 Тогда
		Режимы.Добавить(ПредопределенноеЗначение("РежимЗамещения.Обновление"));
	КонецЕсли;
	
	Возврат (Режимы.Найти(ИспользуемыйРежимЗамещения) <> Неопределено);
КонецФункции

#КонецОбласти
