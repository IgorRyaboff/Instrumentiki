
// --------------------------------------------------------------------------------
// Copyright (c) 2024 Igor Ryabov (https://github.com/IgorRyaboff/Instrumentiki)
// License: https://github.com/IgorRyaboff/Instrumentiki/blob/main/LICENSE
// --------------------------------------------------------------------------------

#Область ОписаниеПеременных
//
#КонецОбласти

#Область ПрограммныйИнтерфейс

#Область БСП

&НаКлиенте
Процедура ВыполнитьКоманду(ИдентификаторКоманды, ОбъектыНазначенияМассив) Экспорт
	Если ИдентификаторКоманды = "ОткрытьОбъектВРедактореОбъектов" Тогда
		Если ОбъектыНазначенияМассив.Количество() > 1 Тогда
			Сообщить(НСтр("ru='Вызов данной команды допустим только при выборе одного объекта'"));
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура("Ссылка", ОбъектыНазначенияМассив[0]);
		ОткрытьФорму("ВнешняяОбработка.Инструментики.Форма.РедакторОбъектов", ПараметрыФормы);
	ИначеЕсли ИдентификаторКоманды = "СравнитьОбъекты" Тогда
		Если ОбъектыНазначенияМассив.Количество() <> 2 Тогда
			Сообщить(НСтр("ru='Для сравнения реквизитов необходимо выделить два объекта в списке'"));
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура("Ссылки", ОбъектыНазначенияМассив);
		ОткрытьФорму("ВнешняяОбработка.Инструментики.Форма.СравнениеРеквизитовОбъектов", ПараметрыФормы);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Объект.Версия) Тогда
		ЭтаФорма.Заголовок = "Инструментики v" + Объект.Версия;
	КонецЕсли;
	
	Элементы.НадписьБезопасныйРежим.Видимость = Объект.БезопасныйРежим;
	Элементы.ФормаПереключитьЭкспериментальныеФункции.Пометка = Объект.ЭкспериментальныеФункции;
	Элементы.НадписьЗащитаОтОпасныхДействий.Видимость = Объект.ВключенаЗащитаОтОпасныхДействий;
	
	СИ = Новый СистемнаяИнформация;
	НаСервереWindows = СтрНачинаетсяС(Строка(СИ.ТипПлатформы), "Windows");
	
	ЗаполнитьСписокИнструментов();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Для Каждого КорневаяСтрока Из СписокИнструментов.ПолучитьЭлементы() Цикл
		Элементы.СписокИнструментов.Развернуть(КорневаяСтрока.ПолучитьИдентификатор());
	КонецЦикла;
	
	СИ = Новый СистемнаяИнформация;
	НаКлиентеWindows = СтрНачинаетсяС(Строка(СИ.ТипПлатформы), "Windows");
	
	ПроверитьОбновленияАсинх();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НадписьЗащитаОтОпасныхДействийОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если Объект.БезопасныйРежим Тогда
		ПоказатьПредупреждение(, НСтр("ru='Данное действие недоступно при работе в безопасном режиме'"));
		Возврат;
	КонецЕсли;
	
	ОтключитьЗащитуОтОпасныхДействийАсинх();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокИнструментов

&НаКлиенте
Процедура СписокИнструментовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТД = Элементы.СписокИнструментов.ТекущиеДанные;
	Если ТД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ПустаяСтрока(ТД.ИмяФормы) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("ВнешняяОбработка.Инструментики.Форма." + ТД.ИмяФормы);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьКаналTelegram(Команда)
	ПараметрыФормы = Новый Структура("Ссылка", "https://t.me/instrumentiki1C");
	ОткрытьФорму("ВнешняяОбработка.Инструментики.Форма.Общая_ВнешняяСсылка", ПараметрыФормы);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИнфостарт(Команда)
	ПараметрыФормы = Новый Структура("Ссылка", "https://infostart.ru/1c/tools/1946049");
	ОткрытьФорму("ВнешняяОбработка.Инструментики.Форма.Общая_ВнешняяСсылка", ПараметрыФормы);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьGitHub(Команда)
	ПараметрыФормы = Новый Структура("Ссылка", "https://github.com/IgorRyaboff/Instrumentiki");
	ОткрытьФорму("ВнешняяОбработка.Инструментики.Форма.Общая_ВнешняяСсылка", ПараметрыФормы);
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПоказатьИнформациюДляТехподдержки(Команда)
	ИнформацияОтСервера = ИнформацияДляТехподдержкиНаСервере();
	
	Результат = Новый Массив;
	
	Версия = ?(ЗначениеЗаполнено(Объект.Версия), Объект.Версия, НСтр("ru='<не заполнена>'"));
	Результат.Добавить("Версия Инструментиков: " + Версия);
	
	Результат.Добавить("Инструментики в безопасном режиме: " + Строка(Объект.БезопасныйРежим));
	Результат.Добавить("");
	
	СистИнфо = Новый СистемнаяИнформация;
	Результат.Добавить("Версия платформы: " + СистИнфо.ВерсияПриложения);
	Результат.Добавить("Тип платформы: " + СистИнфо.ТипПлатформы);
	
	#Если ТолстыйКлиентУправляемоеПриложение Тогда
		Результат.Добавить("Тип клиента: Толстый клиент");
	#ИначеЕсли ТонкийКлиент Тогда
		Результат.Добавить("Тип клиента: Тонкий клиент");
	#ИначеЕсли ВебКлиент Тогда
		Результат.Добавить("Тип клиента: Веб-клиент");
	#Иначе
		Результат.Добавить("Тип клиента: НЛО");
	#КонецЕсли
	Результат.Добавить("Текущий вариант интерфейса: " + Строка(ТекущийВариантИнтерфейсаКлиентскогоПриложения()));
	Результат.Добавить("");
		
	#Если ВебКлиент Тогда
		Результат.Добавить("Браузер: " + СистИнфо.ИнформацияПрограммыПросмотра);
	#Иначе
		Результат.Добавить("ОС клиента: " + СистИнфо.ВерсияОС);
		Результат.Добавить("Процессор клиента: " + СистИнфо.Процессор);
		Результат.Добавить("ОЗУ клиента (Мб): " + СистИнфо.ОперативнаяПамять);
	#КонецЕсли
	Результат.Добавить("");
	
	Результат.Добавить("ОС сервера: " + ИнформацияОтСервера.ОС);
	Результат.Добавить("Процессор сервера: " + ИнформацияОтСервера.Процессор);
	Результат.Добавить("ОЗУ сервера (Мб): " + ИнформацияОтСервера.ОперативнаяПамять);
	Результат.Добавить("");
	
	Результат.Добавить("Конфигурация: " + ИнформацияОтСервера.Конфигурация + " " + ИнформацияОтСервера.ВерсияКонфигурации);
	Результат.Добавить("Режим совместимости основной конфигурации: " + ИнформацияОтСервера.РежимСовместимости);
	Результат.Добавить("Режим совместимости интерфейса основной конфигурации: " + ИнформацияОтСервера.РежимСовместимостиИнтерфейса);
	Результат.Добавить("Режим работы ИБ: " + ИнформацияОтСервера.РежимРаботыИБ);
	
	//ВвестиСтрокуАсинх(СтрСоединить(Результат, Символы.ПС), "Информация для техподдержки",, Истина);
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(СтрСоединить(Результат, Символы.ПС));
	ТекстовыйДокумент.ИспользуемоеИмяФайла = "Информация для техподдержки";
	ТекстовыйДокумент.Показать("Информация для техподдержки");
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПроверитьНаличиеОбновлений(Команда)
	Если Не ЗначениеЗаполнено(Объект.Версия) Тогда
		ВызватьИсключение НСтр("ru='Не заполнена версия обработки'");
	КонецЕсли;
	
	Если Объект.БезопасныйРежим Тогда
		ПоказатьПредупреждение(, "Данная функция недоступна в безопасном режиме
		                         |Для скачивания новой версии запустите Инструментики в небезопасном режиме или перейдите на страницу Инфостарта или канал Telegram",, "Ой, у вас безопасный режим");
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("ВнешняяОбработка.Инструментики.Форма.Главная_Обновление", Новый Структура("ТекущаяВерсия", Объект.Версия));
КонецПроцедуры

&НаКлиенте
Асинх Процедура УказатьПутьККрасивомуРедакторуКода(Команда)
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить("Макет", "Из встроенного в обработку макета");
	Кнопки.Добавить("Путь", "Указать свой путь к внешнему расположению");
	Кнопки.Добавить(Неопределено, "Отмена");
	Ответ = Ждать ВопросАсинх("Откуда брать редактор?", Кнопки,, "Макет");
	Если Ответ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Ответ = "Макет" Тогда
		ЗаписатьПутьККрасивомуРедакторуКода(Неопределено);
	ИначеЕсли Ответ = "Путь" Тогда
		Путь = Ждать ВвестиСтрокуАсинх("", "Укажите путь");
		Если Путь = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ЗаписатьПутьККрасивомуРедакторуКода(СокрЛП(Путь));
	КонецЕсли;
	ПоказатьПредупреждение(, "Путь записан");
КонецПроцедуры

&НаКлиенте
Процедура СкрытьВерсиюИзЗаголовкаФормы(Команда)
	Заголовок = "Инструментики";
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьЭкспериментальныеФункции(Команда)
	ЗаписатьНастройкуЭкспериментальныеФункции(Не Объект.ЭкспериментальныеФункции);
	
	Объект.ЭкспериментальныеФункции = Не Объект.ЭкспериментальныеФункции;
	Элементы.ФормаПереключитьЭкспериментальныеФункции.Пометка = Объект.ЭкспериментальныеФункции;
	
	ПоказатьПредупреждение(, НСтр("ru='Для вступления изменений в силу необходимо перезапустить обработку'"));
КонецПроцедуры

&НаКлиенте
Асинх Процедура ОчиститьВсеНастройкиИнструментиков(Команда)
	Ответ = Ждать ВопросАсинх(НСтр("ru = 'Будут очищены настройки всех инструментов Инструментиков для текущего пользователя.
                                    |Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьВсеНастройкиИнструментиковНаСервере();
	Ждать ПредупреждениеАсинх(НСтр("ru='Все настройки Инструментиков очищены. Перезапустите обработку'"));
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Добавляет описание инструмета в дерево
//
// Параметры:
//  Родитель - ДанныеФормыЭлементДерева - Элемент дерева, внутрь которого будет добавляться строка описания инструмента
//  Наименование - Строка - Выводимое пользователю наименование инструмента
//  ИмяФормы - Строка - Имя формы, открываемое при выборе инструмента. Если не задано, строка будет отображаться с перечёркнутым наименованием
&НаСервере
Процедура ДобавитьИнструмент(КоллекцияЭлементовСтрокиДерева, Наименование, ИмяФормы, Экспериментальный)
	Строка = КоллекцияЭлементовСтрокиДерева.Добавить();
	Строка.Наименование = ?(ЗначениеЗаполнено(ИмяФормы), Наименование, Наименование + " (планируется)");
	Строка.ИмяФормы = ИмяФормы;
	Строка.Экспериментальный = Экспериментальный;
КонецПроцедуры

// Сравнивает две версии
//
// Параметры
//  ВерсияСлева - Строка -
//  ВерсияСправа - Строка -
//
// Возвращаемое значение:
//  Число - "0", если версии совпадают; "1", если ВерсияСлева > ВерсияСправа; "-1", если ВерсияСлева < ВерсияСправа
&НаКлиентеНаСервереБезКонтекста
Функция СравнитьВерсии(ВерсияСлева, ВерсияСправа)
	ЧастиСлева = СтрРазделить(ВерсияСлева, ".");
	ЧастиСправа = СтрРазделить(ВерсияСправа, ".");
	КоличествоЧастей = ?(ЧастиСлева.Количество() > ЧастиСправа.Количество(), ЧастиСлева.Количество(), ЧастиСправа.Количество());
	
	Пока ЧастиСлева.Количество() < КоличествоЧастей Цикл
		ЧастиСлева.Добавить("0");
	КонецЦикла;
	Пока ЧастиСправа.Количество() < КоличествоЧастей Цикл
		ЧастиСправа.Добавить("0");
	КонецЦикла;
	
	Для Сч = 0 По КоличествоЧастей - 1 Цикл
		Слева = Число(ЧастиСлева[Сч]);
		Справа = Число(ЧастиСправа[Сч]);
		
		Если Слева > Справа Тогда
			Возврат -1;
		ИначеЕсли Слева < Справа Тогда
			Возврат 1;
		КонецЕсли;
	КонецЦикла;
	
	Возврат 0;
КонецФункции

// Получить ссылку на официальный Telegram-канал обработки
//
// Возвращаемое значение:
//  Строка -
&НаКлиентеНаСервереБезКонтекста
Функция СсылкаНаКаналTelegram()
	Возврат "https://t.me/instrumentiki1C";
КонецФункции

// Заполнение списка инструментов
//
&НаСервере
Процедура ЗаполнитьСписокИнструментов()
	СписокИнструментов.ПолучитьЭлементы().Очистить();
	
	Таблица = РеквизитФормыВЗначение("Объект").ПолучитьМакет("СписокИнструментов");
	
	ТекКоллекцияЭлементов = Неопределено;
	ТекИмяГруппы = "";
	Для НомерСтроки = 2 По Таблица.ВысотаТаблицы Цикл
		ИмяГруппы = Таблица.Область(НомерСтроки, 1, НомерСтроки, 1).Текст;
		ПредставлениеГруппы = Таблица.Область(НомерСтроки, 2, НомерСтроки, 2).Текст;
		ПредставлениеИнструмента = Таблица.Область(НомерСтроки, 3, НомерСтроки, 3).Текст;
		ИмяФормыИнструмента = Таблица.Область(НомерСтроки, 4, НомерСтроки, 4).Текст;
		Экспериментальный = ЗначениеЗаполнено(Таблица.Область(НомерСтроки, 5, НомерСтроки, 5).Текст);
		
		Если Экспериментальный И Не Объект.ЭкспериментальныеФункции Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПустаяСтрока(ИмяГруппы) Тогда
			Продолжить;
		КонецЕсли;
		Если ИмяГруппы <> ТекИмяГруппы Тогда
			ГруппаДерева = СписокИнструментов.ПолучитьЭлементы().Добавить();
			ТекИмяГруппы = ИмяГруппы;
			ГруппаДерева.Наименование = ПредставлениеГруппы;
			ГруппаДерева.ЭтоГруппа = Истина;
			ТекКоллекцияЭлементов = ГруппаДерева.ПолучитьЭлементы();
		КонецЕсли;
		
		ДобавитьИнструмент(ТекКоллекцияЭлементов, ПредставлениеИнструмента, ИмяФормыИнструмента, Экспериментальный);
	КонецЦикла;
КонецПроцедуры

// Передаёт клиенту информацию для обращения в техподдержку, которую можно получить только на сервере
//
&НаСервере
Функция ИнформацияДляТехподдержкиНаСервере()
	Результат = Новый Структура;
	Результат.Вставить("Конфигурация", Метаданные.Имя);
	Результат.Вставить("ВерсияКонфигурации", Метаданные.Версия);
	Результат.Вставить("РежимСовместимости", Строка(Метаданные.РежимСовместимости));
	Результат.Вставить("РежимСовместимостиИнтерфейса", Строка(Метаданные.РежимСовместимостиИнтерфейса));
	
	СистИнфо = Новый СистемнаяИнформация;
	Результат.Вставить("ОС", СистИнфо.ВерсияОС);
	Результат.Вставить("Процессор", СистИнфо.Процессор);
	Результат.Вставить("ОперативнаяПамять", СистИнфо.ОперативнаяПамять);
	
	Если СтрНачинаетсяС(НРег(СтрокаСоединенияИнформационнойБазы()), "srvr=") Тогда
		РежимРаботыИБ = "Клиент-серверный";
	ИначеЕсли СтрНачинаетсяС(НРег(СтрокаСоединенияИнформационнойБазы()), "file=") Тогда
		РежимРаботыИБ = "Файловый";
	ИначеЕсли СтрНачинаетсяС(НРег(СтрокаСоединенияИнформационнойБазы()), "ws=") Тогда
		РежимРаботыИБ = "Через веб-сервер";
	Иначе
		РежимРаботыИБ = "Неизвестный";
	КонецЕсли;
	
	Результат.Вставить("РежимРаботыИБ", РежимРаботыИБ);
	
	Возврат Результат;
КонецФункции

//
// Параметры:
//  Путь - Строка, Неопределено - Если Неопределено, красивый редактор будет распаковываться из макета. В качестве строки передаётся путь
&НаСервере
Процедура ЗаписатьПутьККрасивомуРедакторуКода(Путь)
	РеквизитФормыВЗначение("Объект").СохранитьНастройку(Неопределено, "ПутьККрасивомуРедакторуКода", Путь);
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПроверитьОбновленияАсинх()
	Если Не ЗначениеЗаполнено(Объект.Версия) Тогда
		Возврат;
	КонецЕсли;
	
	ТелоОтвета = Неопределено;
	Попытка
		Соединение = Новый HTTPСоединение("api.github.com", 443,,,, 5, Новый ЗащищенноеСоединениеOpenSSL);
		
		Запрос = Новый HTTPЗапрос("/repos/IgorRyaboff/Instrumentiki/releases");
		Запрос.Заголовки.Вставить("X-GitHub-Api-Version", "2022-11-28");
		Запрос.Заголовки.Вставить("Accept", "application/vnd.github+json");
		
		HTTPОтвет = Ждать Соединение.ПолучитьАсинх(Запрос);
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(HTTPОтвет.ПолучитьТелоКакСтроку());
		ТелоОтвета = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
	Исключение
		// Ничего не делаем
		Возврат;
	КонецПопытки;
	
	ЕстьОбновление = Ложь;
	Для Каждого ОписаниеВерсии Из ТелоОтвета Цикл
		НомерВерсии = ОписаниеВерсии.tag_name;
		ЭтоПредварительнаяВерсия = ОписаниеВерсии.prerelease;
		
		Если Не ЭтоПредварительнаяВерсия И СравнитьВерсии(Объект.Версия, НомерВерсии) = 1 Тогда
			ЕстьОбновление = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЕстьОбновление Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ПроверитьНаличиеОбновлений.Заголовок = НСтр("ru='Доступно обновление Инструментиков! Нажмите, чтобы загрузить'");
	Элементы.ПроверитьНаличиеОбновлений.ЦветТекста = WebЦвета.Красный;
	
	СообщитьОНаличииОбновленияАсинх();
КонецПроцедуры

&НаКлиенте
Асинх Процедура СообщитьОНаличииОбновленияАсинх()
	Если Объект.НеНапоминатьОбОбновленииДо >= ТекущаяДата() Тогда
		Возврат;
	КонецЕсли;
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить("Обновить", НСтр("ru='Обновить'"));
	Если Объект.ПравоСохранениеНастроек Тогда
		Кнопки.Добавить("НеНапоминать", НСтр("ru='Скрыть на неделю'"));
	КонецЕсли;
	Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru='Закрыть'"));
	Ответ = Ждать ВопросАсинх(НСтр("ru = 'Доступно обновление Инструментиков! Рекомендуется обновить обработку'") , Кнопки,, "Обновить");
		
	Если Ответ = "Обновить" Тогда
		ОткрытьФорму("ВнешняяОбработка.Инструментики.Форма.Главная_Обновление", Новый Структура("ТекущаяВерсия", Объект.Версия));
	ИначеЕсли Ответ = "НеНапоминать" Тогда
		ЗаписатьДатуНеНапоминатьОбОбновлении(КонецДня(ТекущаяДата()) + (86400 * 7));
		Состояние(НСтр("ru='Скрыли напоминания'"),, НСтр("ru='Встретимся через неделю :)'"));
	ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
		// Ничего не делаем, пользователь прервал действие
	Иначе
		ВызватьИсключение "Некорректный ответ " + Ответ;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДатуНеНапоминатьОбОбновлении(Значение)
	РеквизитФормыВЗначение("Объект").СохранитьНастройку(Неопределено, "НеНапоминатьОбОбновленииДо", Значение);
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкуЭкспериментальныеФункции(Значение)
	РеквизитФормыВЗначение("Объект").СохранитьНастройку(Неопределено, "ЭкспериментальныеФункции", Значение);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОчиститьВсеНастройкиИнструментиковНаСервере()
	Если Не ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = ХранилищеОбщихНастроек.Выбрать(Новый Структура("Пользователь", ИмяПользователя()));
	Пока Выборка.Следующий() Цикл
		Если Выборка.КлючОбъекта = "Инструментики" Или СтрНачинаетсяС(Выборка.КлючОбъекта, "Инструментики.") Тогда
			ХранилищеОбщихНастроек.Удалить(Выборка.КлючОбъекта, Неопределено, ИмяПользователя());
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ОтключитьЗащитуОтОпасныхДействийАсинх()
	Ответ = Ждать ВопросАсинх(НСтр("ru='Отключить защиту от опасных действий для текущего пользователя?'"), РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ОтключитьЗащитуОтОпасныхДействийНаСервере();
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Перезапустить сейчас'"));
	Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Перезапустить позднее'"));
	Ответ = Ждать ВопросАсинх(НСтр("ru='Для вступления изменений в силу необходимо перезапустить программу'"), Кнопки);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗавершитьРаботуСистемы(Истина, Истина);
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтключитьЗащитуОтОпасныхДействийНаСервере()
	ПользовательИБ = ПользователиИнформационнойБазы.ТекущийПользователь();
	ПользовательИБ.ЗащитаОтОпасныхДействий.ПредупреждатьОбОпасныхДействиях = Ложь;
	ПользовательИБ.Записать();
КонецПроцедуры

#КонецОбласти
