
// --------------------------------------------------------------------------------
// Copyright (c) 2024 Igor Ryabov (https://github.com/IgorRyaboff/Instrumentiki)
// License: https://github.com/IgorRyaboff/Instrumentiki/blob/main/LICENSE
// --------------------------------------------------------------------------------

#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Ссылка") Тогда
		Ссылка = Параметры.Ссылка;
	Иначе
		Ссылка = "https://t.me/instrumentiki1C";
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КартинкаQRКодаНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если Объект.БезопасныйРежим Тогда
		ПоказатьПредупреждение(, "Получение QR-кода недоступно в безопасном режиме",, "Безопасный режим");
		Возврат;
	КонецЕсли;
	
	Состояние("Получение QR-кода ссылки...");
	Попытка
		ПолучитьQRКод();
		Состояние("QR-код получен");
	Исключение
		ПоказатьПредупреждение(, ОписаниеОшибки(),, "Ошибка получения QR-кода");
		Состояние("QR-код не получен");
	КонецПопытки;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерейтиПоСсылке(Команда)
	ПерейтиПоНавигационнойСсылке(Ссылка);
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получает картинку QR-кода, помещает её во временное хранилище и заносит адрес в реквизит АдресКартинки
//
&НаСервере
Процедура ПолучитьQRКод()
	Соединение = Новый HTTPСоединение("quickchart.io", 443,,,, 5, Новый ЗащищенноеСоединениеOpenSSL);
	Запрос = Новый HTTPЗапрос("chart?cht=qr&chs=100x100&chld=L|0&chl=" + КодироватьСтроку(Ссылка, СпособКодированияСтроки.КодировкаURL));
	ОтветХТТП = Соединение.ВызватьHTTPМетод("GET", Запрос);
	
	ДвДанные = ОтветХТТП.ПолучитьТелоКакДвоичныеДанные();
	Картинка = Новый Картинка(ДвДанные, Истина);
	
	АдресКартинки = ПоместитьВоВременноеХранилище(Картинка, ЭтотОбъект.УникальныйИдентификатор);
	
	Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	Элементы.КартинкаQRКода.Рамка = Рамка;
	Элементы.КартинкаQRКода.Гиперссылка = Ложь;
КонецПроцедуры

#КонецОбласти
