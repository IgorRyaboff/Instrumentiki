
// --------------------------------------------------------------------------------
// Copyright (c) 2024 Igor Ryabov (https://github.com/IgorRyaboff/Instrumentiki)
// License: https://github.com/IgorRyaboff/Instrumentiki/blob/main/LICENSE
// --------------------------------------------------------------------------------

#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	//
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Расшифровать(Команда)
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		РезультатРазбора = РазобратьТекстБитойСсылки();
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не удалось разобрать текст. Проверьте его корректность'");
		Сообщение.Поле = "Текст";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Сообщить();
		Возврат;
	КонецПопытки;
	
	Если СоответствиеИменТаблицИОбъектовМетаданных = Неопределено Тогда
		Состояние(НСтр("ru='Чтение структуры базы данных...'"));
		СоответствиеИменТаблицИОбъектовМетаданных = СоответствиеИменТаблицИОбъектовМетаданных();
		Состояние();
	КонецЕсли;
	
	НомерТаблицы = РезультатРазбора.НомерТаблицы;
	УникальныйИдентификаторОбъекта = РезультатРазбора.УникальныйИдентификатор;
	
	Если СоответствиеИменТаблицИОбъектовМетаданных.Получить(НомерТаблицы) = Неопределено Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Указанная в таблице ссылка не найдена в структуре базы данных. Вероятно, указанная ссылка не принадлежит этой конфигурации'");
		Сообщение.Поле = "Текст";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	РезультатРасшифровки = СсылкаПоНомеруТаблицыИИдентификатору(НомерТаблицы, УникальныйИдентификаторОбъекта, СоответствиеИменТаблицИОбъектовМетаданных);
	ОбъектМетаданных = РезультатРасшифровки.Метаданные;
	Ссылка = РезультатРасшифровки.Ссылка;
	НавигационнаяСсылкаОбъекта = ПолучитьНавигационнуюСсылку(Ссылка);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция СсылкаПоНомеруТаблицыИИдентификатору(Знач НомерТаблицы, Знач УникальныйИдентификаторОбъекта, Знач СоответствиеИменТаблицИОбъектовМетаданных)
	ПолноеИмяОбъекта = СоответствиеИменТаблицИОбъектовМетаданных[НомерТаблицы];
	МенеджерОбъекта = Новый(СтрЗаменить(ПолноеИмяОбъекта, ".", "Менеджер."));
	Ссылка = МенеджерОбъекта.ПолучитьСсылку(УникальныйИдентификаторОбъекта);
	
	Результат = Новый Структура("Метаданные, Ссылка", ПолноеИмяОбъекта, Ссылка);
	Возврат Результат;
КонецФункции

&НаСервереБезКонтекста
Функция СоответствиеИменТаблицИОбъектовМетаданных()
	СтруктураХранения = ПолучитьСтруктуруХраненияБазыДанных();
	СоответствиеИменТаблицИОбъектовМетаданных = Новый Соответствие;
	
	Отбор = Новый Структура("Назначение", "Основная");
	Цифры = СтрРазделить("0 1 2 3 4 5 6 7 8 9", " ");
	Для Каждого СтрСтруктура Из СтруктураХранения.НайтиСтроки(Отбор) Цикл
		НомерТаблицы = "";
		Для НомерСимвола = 1 По СтрДлина(СтрСтруктура.ИмяТаблицыХранения) Цикл
			Если Цифры.Найти(Сред(СтрСтруктура.ИмяТаблицыХранения, НомерСимвола, 1)) <> Неопределено Тогда
				НомерТаблицы = Сред(СтрСтруктура.ИмяТаблицыХранения, НомерСимвола);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		СоответствиеИменТаблицИОбъектовМетаданных[НомерТаблицы] = СтрСтруктура.Метаданные;
	КонецЦикла;
	
	Возврат Новый ФиксированноеСоответствие(СоответствиеИменТаблицИОбъектовМетаданных);
КонецФункции

Функция РазобратьТекстБитойСсылки()
	ИдентификационныеДанныеТекста = СтрРазделить(СтрРазделить(Текст, "(")[1], ")")[0];
	
	ЧастиТекста = СтрРазделить(ИдентификационныеДанныеТекста, ":");
	НомерТаблицы = ЧастиТекста[0];
	НеразобранныйИдентификатор = ЧастиТекста[1];
	
	// Источник: https://helpf.pro/faq/view/483.html
	ИдентификаторОбъекта = Сред(НеразобранныйИдентификатор, 25, 8)
		+ "-" + Сред(НеразобранныйИдентификатор, 21, 4)
		+ "-" + Сред(НеразобранныйИдентификатор, 17, 4)
		+ "-" + Сред(НеразобранныйИдентификатор, 1, 4)
		+ "-" + Сред(НеразобранныйИдентификатор, 5, 12);
	//
	УникальныйИдентификаторОбъекта = Новый УникальныйИдентификатор(ИдентификаторОбъекта);
	
	Результат = Новый Структура("УникальныйИдентификатор, НомерТаблицы", УникальныйИдентификаторОбъекта, НомерТаблицы);
	Возврат Результат;
КонецФункции

#КонецОбласти
